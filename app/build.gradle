apply plugin: 'com.android.application'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.landenlabs.all_flipanimation"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 10115
        versionName '1.01.15'

        // Limit languages to following list.
        resConfigs "en", "es"

        ndk {
            // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Save app version in string so resource can be used in layouts
    applicationVariants.all { variant ->
        variant.resValue "string", "versionName", "version " + variant.versionName
    }

    lintOptions {
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute'
    }

    productFlavors {
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            // Enable verbose lint reporting during build.
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
}

// Rename app
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def ourname = project.parent.name
        outputFileName = "$ourname-${variant.name}-${variant.versionName}.apk"
        println "out name= $outputFileName"
    }
}